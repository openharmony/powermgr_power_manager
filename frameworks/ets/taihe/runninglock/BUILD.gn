# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../powermgr.gni")

copy_taihe_idl("copy_power_manager_runninglock") {
  sources = [ "idl/ohos.runningLock.taihe" ]
}

subsystem_name = "powermgr"
part_name = "power_manager"
taihe_generated_file_path_runningLock =
    "$taihe_file_path/out/$subsystem_name/$part_name/runningLock"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_runningLock}"
  deps = [ ":copy_power_manager_runninglock" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.runningLock.ani.cpp",
    "$taihe_generated_file_path/src/ohos.runningLock.abi.c",
  ]
}

taihe_shared_library("power_manager_runninglock_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path_runningLock}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.runningLock.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${powermgr_inner_api}:powermgr_client",
  ]
  external_deps = [ "hilog:libhilog" ]
}

generate_static_abc("runningLock_abc") {
  base_url = "${taihe_generated_file_path_runningLock}"
  files = [ "${taihe_generated_file_path_runningLock}/@ohos.runningLock.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/runningLock_abc.abc"
  dependencies = [ ":run_taihe" ]
}

generate_static_abc("runningLock_test_abc") {
  base_url = "$powermgr_framework/ets/taihe/runninglock/test"
  files = [ "$powermgr_framework/ets/taihe/runninglock/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/runningLock_test_abc.abc"
}

ohos_prebuilt_etc("runningLock_etc") {
  source = "$target_out_dir/runningLock_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":runningLock_abc" ]
}
