# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("../../powermgr.gni")

config("powermgr_public_config") {
  include_dirs = [
    "native/include",
    "${powermgr_service_zidl}/include",
  ]
}

config("powermgr_private_config") {
  include_dirs = [
    "${powermgr_framework_native}",
    "${powermgr_service_zidl}/src/shutdown",
  ]
}

config("powermgr_ipc_config") {
  include_dirs = [
    "native/include",
    "native/include/hibernate",
    "native/include/shutdown",
    "native/include/suspend",
    "native/include/ulsr",
    "${powermgr_service_path}/native/include",
    "${powermgr_service_zidl}/include",
    "${target_gen_dir}",
  ]
}

idl_gen_interface("powermgr_interface") {
  sources = [ "IPowerMgr.idl" ]

  log_domainid = "0xD002902"
  log_tag = "PowerSvc"
  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}

ohos_source_set("powermgr_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":powermgr_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]

  public_configs = [ ":powermgr_ipc_config" ]

  deps = [ ":powermgr_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_single",
  ]

  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}

ohos_source_set("powermgr_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":powermgr_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])
  sources += [
    "${powermgr_service_zidl}/src/power_mgr_async_reply.cpp",
    "${powermgr_service_zidl}/src/power_mode_callback_stub.cpp",
    "${powermgr_service_zidl}/src/power_runninglock_callback_stub.cpp",
    "${powermgr_service_zidl}/src/power_state_callback_stub.cpp",
    "${powermgr_service_zidl}/src/screen_off_pre_callback_stub.cpp",
    "${powermgr_service_zidl}/src/sync_sleep_callback_stub.cpp",
  ]

  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]

  public_configs = [ ":powermgr_ipc_config" ]

  deps = [
    ":powermgr_interface",
    ":powermgr_client",
  ]

  external_deps = [
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_single",
  ]

  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}

ohos_shared_library("powermgr_client") {
  branch_protector_ret = "pac_ret"

  sources = [
    "${powermgr_framework_native}/client_lifecycle.cpp",
    "${powermgr_framework_native}/power_mgr_client.cpp",
    "${powermgr_framework_native}/running_lock.cpp",
    "${powermgr_framework_native}/running_lock_info.cpp",
    "${powermgr_framework_native}/shutdown/shutdown_client.cpp",
    "${powermgr_framework_native}/shutdown/takeover_info.cpp",
    "${powermgr_service_zidl}/src/async_ulsr_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/async_ulsr_callback_stub.cpp",
    "${powermgr_service_zidl}/src/power_mgr_async_reply.cpp",
    "${powermgr_service_zidl}/src/power_mode_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/power_mode_callback_stub.cpp",
    "${powermgr_service_zidl}/src/power_runninglock_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/power_runninglock_callback_stub.cpp",
    "${powermgr_service_zidl}/src/power_state_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/power_state_callback_stub.cpp",
    "${powermgr_service_zidl}/src/screen_off_pre_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/screen_off_pre_callback_stub.cpp",
    "${powermgr_service_zidl}/src/shutdown/async_shutdown_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/shutdown/async_shutdown_callback_stub.cpp",
    "${powermgr_service_zidl}/src/shutdown/sync_shutdown_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/shutdown/sync_shutdown_callback_stub.cpp",
    "${powermgr_service_zidl}/src/shutdown/takeover_shutdown_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/shutdown/takeover_shutdown_callback_stub.cpp",
    "${powermgr_service_zidl}/src/sync_hibernate_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/sync_hibernate_callback_stub.cpp",
    "${powermgr_service_zidl}/src/sync_sleep_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/sync_sleep_callback_stub.cpp",
    "${powermgr_service_zidl}/src/takeover_suspend_callback_proxy.cpp",
    "${powermgr_service_zidl}/src/takeover_suspend_callback_stub.cpp",
  ]

  output_values = get_target_outputs(":powermgr_interface")
  sources += filter_include(output_values, [ "*_proxy.cpp" ])

  configs = [
    "${powermgr_utils_path}:coverage_flags",
    ":powermgr_ipc_config",
    ":powermgr_private_config",
  ]

  public_configs = [
    ":powermgr_ipc_config",
    ":powermgr_public_config",
    "${powermgr_utils_path}:utils_config",
  ]

  deps = [ ":powermgr_interface" ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "powermgr"
  innerapi_tags = [
    "platformsdk",
    "sasdk",
  ]
  part_name = "${powermgr_part_name}"
}
